<%
  # API
  my $api = gitprep_api;
  
  # Parameters
  my $user = param('user');
  my $project = param('project');
  my $object = param('object');

  # Git
  my $git = $self->app->git;
  
  # Id and file
  my ($rev, $blob) = @{$git->parse_object($user, $project, $object)};

  # Commit
  my $commit_log = $git->latest_commit_log($user, $project, $rev, $blob);
  
  # Blob content
  my $bid = $git->id_by_path($user, $project, $rev, $blob, 'blob')
    or $api->croak('Cannot find file');
  my @cmd = $git->_cmd(
    $user,
    $project,
    'cat-file',
    'blob',
    $bid
  );
  open my $fh, '-|', @cmd
    or $api->croak(qq/Couldn't cat "$blob", "$bid"/);
  
  # Blob plain
  my $commit;
  my $mimetype;
  my $lines =[];
  
  # Content type
  my $type = $git->blob_contenttype($fh, $blob);

  # Convert text/* content type to text/plain
  if ($self->config('prevent_xss') &&
    ($type =~ m#^text/[a-z]+\b(.*)$# ||
    ($type =~ m#^[a-z]+/[a-z]\+xml\b(.*)$# && -T $fh)))
  {
    my $rest = $1;
    $rest = defined $rest ? $rest : '';
    $type = "text/plain$rest";
  }

  # File name
  my $blob_name = $rev;
  if (defined $blob) { $blob_name = $blob }
  elsif ($type =~ m/^text\//) { $blob_name .= '.txt' }
  
  # Content
  my $content = do { local $/; <$fh> };
  my $sandbox = $self->config('prevent_xss') &&
    $type !~ m#^(?:text/[a-z]+|image/(?:gif|png|jpeg))(?:[ ;]|$)#;
  my $content_disposition = $sandbox ? 'attachment' : 'inline';
  $content_disposition .= "; filename=$blob_name";
  
  # Render
  $self->res->headers->content_disposition($content_disposition);
  $self->res->headers->content_type($type);
  
  return $content;
%>
