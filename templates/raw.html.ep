<%
  # Parameters
  my $user = param('user');
  my $project = param('project');
  my $rev = param('rev');
  my $file = param('file');

  # Git
  my $git = app->git;
  
  # Blob raw
  my $blob_raw = $git->blob_raw($user, $project, $rev, $file);
  
  # Content type
  my $type = $git->blob_contenttype($user, $project, $rev, $file);

  # Convert text/* content type to text/plain
  if (app->config->{basic}{prevent_xss} &&
    ($type =~ m#^text/[a-z]+\b(.*)$# ||
    ($type =~ m#^[a-z]+/[a-z]\+xml\b(.*)$#)))
  {
    my $rest = $1;
    $rest = defined $rest ? $rest : '';
    $type = "text/plain$rest";
  }

  # File name
  my $file_name = $rev;
  if (defined $file) { $file_name = $file }
  elsif ($type =~ m/^text\//) { $file_name .= '.txt' }
  
  # Content disposition
  my $sandbox = app->config->{basic}{prevent_xss} &&
    $type !~ m#^(?:text/[a-z]+|image/(?:gif|png|jpeg))(?:[ ;]|$)#;
  my $content_disposition = $sandbox ? 'attachment' : 'inline';
  $content_disposition .= "; filename=$file_name";
  
  # Response
  $self->res->headers->content_disposition($content_disposition);
  $self->res->headers->content_type($type);
  $self->render(data => $blob_raw);
  $self->finish_rendering;
  
  return;
%>
