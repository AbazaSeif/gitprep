<%
  # API
  my $api = gitprep_api;
  
  # Parameters
  my $user = param('user');
  my $project = param('project');
  my $rev = param('rev');
  my $file = param('file');

  # Git
  my $git = $self->app->git;

  # Commit
  my $commit_log = $git->latest_commit_log($user, $project, $rev, $file);
  
  # Blob content
  my $bid = $git->id_by_path($user, $project, $rev, $file, 'blob')
    or $api->croak('Cannot find file');
  my @cmd = $git->cmd(
    $user,
    $project,
    'cat-file',
    'blob',
    $bid
  );
  open my $fh, '-|', @cmd
    or $api->croak(qq/Couldn't cat "$file", "$bid"/);
  
  # Blob plain
  my $commit;
  my $mimetype;
  my $lines =[];
  
  # Content type
  my $type = $git->blob_contenttype($fh, $file);

  # Convert text/* content type to text/plain
  if ($self->config('prevent_xss') &&
    ($type =~ m#^text/[a-z]+\b(.*)$# ||
    ($type =~ m#^[a-z]+/[a-z]\+xml\b(.*)$# && -T $fh)))
  {
    my $rest = $1;
    $rest = defined $rest ? $rest : '';
    $type = "text/plain$rest";
  }

  # File name
  my $file_name = $rev;
  if (defined $file) { $file_name = $file }
  elsif ($type =~ m/^text\//) { $file_name .= '.txt' }
  
  # Content
  my $content = do { local $/; <$fh> };
  my $sandbox = $self->config('prevent_xss') &&
    $type !~ m#^(?:text/[a-z]+|image/(?:gif|png|jpeg))(?:[ ;]|$)#;
  my $content_disposition = $sandbox ? 'attachment' : 'inline';
  $content_disposition .= "; filename=$file_name";
  
  # Render
  $self->res->headers->content_disposition($content_disposition);
  $self->res->headers->content_type($type);
  
  $self->render(text => $content);
  
  return $self->res->body;
%>
