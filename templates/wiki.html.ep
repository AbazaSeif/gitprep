<%
  # API
  my $api = gitprep_api;

  # Parameters
  my $user_id = param('user');
  my $project_id = param('project');
  my $title = param('title');
  my $new = param('new');
  my $edit = param('edit');
  my $list_pages = param('list-pages');
  
  my $project_row_id = $api->get_project_row_id($user_id, $project_id);
  
  my $wiki = app->dbi->model('wiki')->select(where => {project => $project_row_id})->one;
  
  my $content;
  my $content_md;
  my $pages;
  
  # Logined
  my $logined = $api->logined;

  # Wiki repository URL
  my $url = url_for->to_abs;
  $url->base(undef);
  my $ssh_port = config->{basic}{ssh_port};
  my $rep_home = app->rep_home;
  my $execute_user = getpwuid($>);
  my $ssh_rep_url_base = defined app->config->{basic}{'ssh_rep_url_base'}
    ? app->config->{basic}{'ssh_rep_url_base'} : $rep_home;
  my $ssh_rep_url = "ssh://$execute_user\@" . $url->host
    . ($ssh_port ? ":$ssh_port" : '') . "$ssh_rep_url_base/$user_id/$project_id.wiki.git";
  
  # Display type
  my $display;
  
  my $errors;
  if ($self->req->method eq 'POST') {
    my $op = param('op') // '';
    
    if ($op eq 'create') {
      
      my $title = param('title');
      my $content = param('content');
      my $commit_message = param('commit-message');
      
      # Validation
      my $vc = app->vc;
      my $validation = $vc->validation;
      
      # Title
      if (!length $title) {
        $validation->add_failed(title => 'Page title is empty.');
      }
      elsif ($title =~ /^_/) {
        if ($title ne '_Sidebar' && $title ne '_Footer') {
          $validation->add_failed(title => 'Page title can\'t start with _.');
        }
      }
      
      if (!length $commit_message) {
        $commit_message = "Updated $title";
      }
      
      if ($validation->is_valid) {
        $api->create_wiki_page($user_id, $project_id, $title, $content, $commit_message);
        
        $self->redirect_to("/$user_id/$project_id/wiki/$title");
        return;
      }
      else {
        $errors = $validation->messages;
      }
    }
  }

  my $exists_sidebar;
  my $exists_footer;
  my $content_sidebar_md;
  my $content_footer_md;
  if ($wiki) {
    if ($new) {
      $display = 'new';
    }
    else {
      if ($edit) {
        $content = $api->get_wiki_page_content($user_id, $project_id, $title);
        if (defined $content) {
          $display = 'edit';
        }
        else {
          $display = 'new';
        }
      }
      elsif ($list_pages) {
        $display = 'list-pages';
        # Pages
        $pages = $api->get_wiki_pages($user_id, $project_id);
      }
      else {
        if (($title // '') eq 'Home') {
          $self->redirect_to("/$user_id/$project_id/wiki");
          return;
        }
        else {
          $title //= 'Home';
          $content = $api->get_wiki_page_content($user_id, $project_id, $title);
          
          if (defined $content) {
            # Convert wiki link to markdonw link
            my $url_base = $self->url_for("/$user_id/$project_id/wiki");
            
            my $re_cb = sub {
              my ($link_text, $title) = @_;
              
              # [[Link text|Title]]
              # [[Title]]
              if (!length $title) {
                $title = $link_text;
              }
              
              my $replace = "[$link_text](" . $url_base . "\/$title)";
              
              my $exists_page = $api->exists_wiki_page($user_id, $project_id, $title);
              
              unless ($exists_page) {
                $replace = '<span class="wiki-link-no-title">' . $replace . '</span>';
              }
              
              return $replace;
            };

            $content =~ s/\[\[([^\]\|]+?)(?:\|([^\[\]]+?))?\]\]/$re_cb->($1, $2)/eg;
            
            $content_md = $api->markdown($content);
            $display = 'page';
            
            # Side bar
            $exists_sidebar = $api->exists_wiki_page($user_id, $project_id, '_Sidebar');
            if ($exists_sidebar) {
              my $content_sidebar = $api->get_wiki_page_content($user_id, $project_id, '_Sidebar');
              $content_sidebar_md = $api->markdown($content_sidebar);
            }
            
            # Footer
            $exists_footer = $api->exists_wiki_page($user_id, $project_id, '_Footer');
            if ($exists_footer) {
              my $content_footer = $api->get_wiki_page_content($user_id, $project_id, '_Footer');
              $content_footer_md = $api->markdown($content_footer);
            }
          }
          else {
            $display = 'new';
          }
        }
      }
    }
  }
  else {
    $display = 'init';
  }
  
  layout 'common', title => "Wiki - $user_id/$project_id";
%>

  %= javascript '/js/icon-input.js';

  %= javascript begin
    $(document).ready(function () {
      
      // Initialize icon input
      init_icon_input();
      
      // Repository URL buttons
      var http_rep_url = '<%= url_for("$user/$project.wiki.git")->to_abs %>';
      
      var logined = <%= $logined ? 'true' : 'false' %>;
      var ssh_rep_url = '<%= $ssh_rep_url %>';
      
      // Click HTTP button
      $('#btn_http').on('click', function () {
        $('#rep_url').val(http_rep_url);
        $('#btn_http').addClass('btn-info');
        $('#btn_ssh').removeClass('btn-info');
      });

      // Click SSH button
      $('#btn_ssh').on('click', function () {
        $('#rep_url').val(ssh_rep_url);
        $('#btn_http').removeClass('btn-info');
        $('#btn_ssh').addClass('btn-info');
      });

      // Initialize
      if (logined) {
        $('#btn_ssh').trigger('click');
      }
      else {
        $('#btn_http').trigger('click');
      }
    });
  % end

  %= include '/include/header';
  
  <div class="container">
    
    <div class="wiki">
      <div>
        <%= include '/include/errors', errors => $errors %>
        <%= include '/include/message', message => flash('message') %>
      </div>
      
      <div class="wiki-top">
        <div class="wiki-top-git-url">
          % if ($display eq 'page' && $title eq 'Home') {
            <ul class="git-url-container">
              % if ($logined) {
                <li>
                  <button class="btn btn-info" id="btn_ssh">SSH</button>
                </li>
              % }
              <li>
                <button class="btn <%= !$logined ? 'btn-info' : '' %>" id="btn_http"><%= $self->req->is_secure ? 'HTTPS' : 'HTTP' %></button>
              </li>
              <li>
                <input id="rep_url" type="text">
              </li>
            </ul>
          % }
          
          % if ($display eq 'page' && $title ne 'Home') {
            <div class="wiki-home-link">
              <a href="<%= url_for("/$user_id/$project_id/wiki") %>">Home</a>
            </div>
          % }
        </div>
        
        <div class="wiki-top-buttons-box">
          <ul class="wiki-top-buttons">
            % if ($display eq 'page') {
              <li>
                <a href="<%= url_for("/$user_id/$project_id/wiki/$title/_edit") %>">
                  <button class="btn btn-new">Edit</button>
                </a>
              </li>
            % }
            % if ($display ne 'new' && $display ne 'init') {
              <li>
                <a href="<%= url_for("/$user_id/$project_id/wiki/_new") %>">
                  <button class="btn btn-green btn-new">Create page</button>
                </a>
              </li>
            % }
          </ul>
        </div>
      </div>
      
      <div class="wiki-container">
        % if ($display eq 'init') {
          <h1 class="topic1">Wiki</h1>
          <div>
            <a href="<%= url_for("/$user_id/$project_id/wiki/_new") %>">
              <button class="btn btn-green btn-new">Create page</button>
            </a>
          </div>
        % } elsif ($display eq 'new' || $display eq 'edit') {
          <%
            my $h1_text;
            my $submit_button_title;
            if ($display eq 'new') {
              $h1_text = 'Create page';
              $submit_button_title = 'Create page';
            }
            else {
              $h1_text = 'Edit page';
              $submit_button_title = 'Save page';
            }
          %>
          
          <h1 class="topic1"><%= $h1_text %></h1>
          <div class="wiki-edit-form">
            <form action="<%= url_for %>" method="POST">
              <%= hidden_field op => 'create' %>
              <label>
                <div class="wiki-edit-form-title">
                  % param(title => $title);
                  %= text_field 'title', placeholder => 'Title';
                </div>
              </label>
              <div class="wiki-add-comment">
                <div class="wiki-add-comment-header">
                  <div class="wiki-message-write-tab wiki-add-comment-header-tab"><a href="javascript:void(0)">Write</a></div>
                  <div class="wiki-message-preview-tab wiki-add-comment-header-tab"><a class="disable" href="javascript:void(0)">Preview</a></div>
                  <div class="wiki-add-comment-icon">
                    <i class="icon-add-header-text icon icon-font" title="Add header text"></i>
                    <i class="icon-add-bold-text icon icon-bold" title="Add bold text"></i>
                    <i class="icon-add-italic-text icon icon-italic" title="Add italic text"></i>
                    <span style="margin-left:10px"></span>
                    <i class="icon-insert-quote icon icon-comment" title="Insert a quote"></i>
                    <i class="icon-insert-code icon icon-file" title="Insert a code"></i>
                    <i class="icon-add-link icon icon-map-marker" title="Add a link"></i>
                    <span style="margin-left:10px"></span>
                    <i class="icon-add-bulleted-list icon icon-list" title="Add a bulleted list"></i>
                    <i class="icon-add-numbered-list icon icon-th-list" title="Add a numbered list"></i>
                    <span style="margin-left:10px"></span>
                    <i class="icon-mension-user icon icon-user" title="Directory mension a user"></i>
                    <!--
                      <i class="icon-reference-issue icon icon-bookmark" title="Reference a issue or pull request"></i>
                    -->
                  </div>
                </div>
                <div class="wiki-add-comment-body">
                  <div class="wiki-message-write-area wiki-add-comment-message">
                    %= text_area 'content' => $content;
                  </div>
                  <div class="wiki-message-preview-area wiki-add-comment-preview markdown-body" style="padding:10px">
                  </div>
                </div>
              </div>
              <label>
                <div class="wiki-edit-form-commit-message-description">
                  Edit message(Optional)
                </div>
                <div class="wiki-edit-form-commit-message">
                  %= text_field 'commit-message';
                </div>
              </label>
              <div class="wiki-edit-form-create-page-btn">
                <%= submit_button $submit_button_title, class => 'btn btn-green btn-new' %>
              </div>
            </form>
          </div>
        % } elsif ($display eq 'page') {
          <div class="wiki-left">
            <div class="wiki-header">
              <%= $title %>
            </div>
            <div class="wiki-frame">
              <div class="markdown-body"><%== $content_md %></div>
            </div>
            % if ($exists_footer) {
              <div class="wiki-footer">
                <%== $content_footer_md %>
              </div>
            % } elsif ($title ne '_Sidebar' && $title ne '_Footer') {
              <div class="wiki-add-footer">
                <a href="<%= url_for("/$user_id/$project_id/wiki/_Footer") %>">+ Add footer</a>
              </div>
            % }
          </div>
          <div class="wiki-right">
            <ul class="wiki-list-pages-box">
              <li>
                <a href="<%= url_for("/$user_id/$project_id/wiki/_pages") %>">Pages</a>
              </li>
            </ul>
            % if ($exists_sidebar) {
              <div class="wiki-side-bar">
                <%== $content_sidebar_md %>
              </div>
            % } elsif ($title ne '_Sidebar' && $title ne '_Footer') {
              <div class="wiki-add-side-bar">
                <a href="<%= url_for("/$user_id/$project_id/wiki/_Sidebar") %>">+ Add side bar</a>
              </div>
            % }
          </div>
        % } elsif ($display eq 'list-pages') {
          <div class="wiki-left">
            <div class="wiki-header">
              Pages
            </div>
            <div class="wiki-frame">
              <ul class="wiki-list-pages">
                % for my $page (@$pages) {
                  <li>
                    <a href="<%= url_for("/$user_id/$project_id/wiki/$page") %>"><%= $page %></a>
                  </li>
                % }
              </ul>
            </div>
          </div>
        % }
      </div>
    </div>
  </div>
  
  %= include '/include/footer';
